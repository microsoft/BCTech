// all queries below filter on environmentType to only include Production


// Note that average execution time is calculated over the signal emitted by the server (for performance reasons only sql calls that takes longer than a server threshold are emitted)

// overview across all tenants and environments
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == 'Long Running Operation (SQL Query)' // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
// | where customDimensions.eventCode == 'TODO - coming in a later version of the schema'
| where customDimensions.alObjectId > 0 // filter out internal server calls
| extend aadID = customDimensions.aadTenantId
| extend environmentName=cloud_RoleInstance 
, environmentType = tostring( customDimensions.environmentType )
, alObjectName = tostring( customDimensions.alObjectName )
, alObjectType = tostring( customDimensions.alObjectType )
, clientType = tostring( customDimensions.clientType )
, executionTimeInMS = toreal(totimespan(customDimensions.executionTime))/10000 //the datatype for executionTime is timespan 
, extensionName = tostring( customDimensions.extensionName )
, sqlStatement = tostring( customDimensions.sqlStatement )
| where environmentType == 'Production'
| summarize countOfExecutions = count(), max(executionTimeInMS), avg(executionTimeInMS) by alObjectName, alObjectType, clientType, extensionName, sqlStatement  
| order by avg_executionTimeInMS desc


// Tenant and environment overview
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == 'Long Running Operation (SQL Query)' // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
// | where customDimensions.eventCode == 'TODO - coming in a later version of the schema'
| where customDimensions.alObjectId > 0 // filter out internal server calls
| extend aadID = tostring( customDimensions.aadTenantId )
, environmentName=cloud_RoleInstance 
, environmentType = tostring( customDimensions.environmentType )
, alObjectName = tostring( customDimensions.alObjectName )
, alObjectType = tostring( customDimensions.alObjectType )
, clientType = tostring( customDimensions.clientType )
, executionTimeInMS = toreal(totimespan(customDimensions.executionTime))/10000 //the datatype for executionTime is timespan 
, extensionName = tostring( customDimensions.extensionName )
| where environmentType == 'Production'
| summarize countOfExecutions = count(), max(executionTimeInMS), avg(executionTimeInMS) by aadID, environmentName, alObjectName, alObjectType, clientType, extensionName  
| order by avg_executionTimeInMS desc

// SQL stats 
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == 'Long Running Operation (SQL Query)' // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
// | where customDimensions.eventCode == 'TODO - coming in a later version of the schema'
| where customDimensions.alObjectId > 0 // filter out internal server calls
| extend aadID = tostring( customDimensions.aadTenantId )
| extend sqlStatement = tostring( customDimensions.sqlStatement )
| extend environmentName=cloud_RoleInstance 
, environmentType = tostring( customDimensions.environmentType )
, alObjectName = tostring( customDimensions.alObjectName )
, alObjectType = tostring( customDimensions.alObjectType )
, clientType = tostring( customDimensions.clientType )
, executionTimeInMS = toreal(totimespan(customDimensions.executionTime))/10000 //the datatype for executionTime is timespan 
, extensionName = tostring( customDimensions.extensionName )
, numberOfJoins = countof(sqlStatement, "JOIN") 
, numberOfOuterApplys = countof(sqlStatement, "OUTER APPLY") 
, alStackTrace = tostring( customDimensions.alStackTrace )
| where environmentType == 'Production'
| summarize totalexecutionTimeInSec = sum(executionTimeInMS)/1000, countOfExecutions = count() by extensionName, alObjectName, sqlStatement, numberOfJoins, numberOfOuterApplys, alStackTrace
| project extensionName, alObjectName, sqlStatement, countOfExecutions, totalexecutionTimeInSec, numberOfJoins, numberOfOuterApplys, alStackTrace
| order by totalexecutionTimeInSec desc