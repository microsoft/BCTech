// KQL samples - Report execution

// Use renames in the project operator if you don't need to filter on the columns extracted from customDimensions
// move columns up to the extend operator and add a tostring function call if you need to filter or summarize on it

// successful report execution (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Success report generation" // do note that in a later version of the schema, this field will not be used 
     or customDimensions.eventId == 'RT0006' // introduced in version 16.1
| where customDimensions.result == "Success"
| extend aadID = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
| project 
  eventId = customDimensions.eventId 
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
, serverExecutionTime = customDimensions.serverExecutionTime
, serverExecutionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, totalTime = customDimensions.totalTime
, totalTimeInMS = toreal(totimespan(customDimensions.totalTime))/10000 //the datatype for executionTime is timespan 
, numberOfRows = customDimensions.numberOfRows
, result = customDimensions.result
, message, customDimensions


// failed report execution (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Failed report generation" // do note that in a later version of the schema, this field will not be used 
  or customDimensions.eventId == 'RT0006' // introduced in version 16.1
| where failurcustomDimensions.result <> "Success"
| extend aadID = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
| project
  eventId = customDimensions.eventId
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
, failureReason = customDimensions.result
, serverExecutionTime = customDimensions.serverExecutionTime
, serverExecutionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, totalTime = customDimensions.totalTime
, totalTimeInMS = toreal(totimespan(customDimensions.totalTime))/10000 //the datatype for executionTime is timespan 
, numberOfRows = customDimensions.numberOfRows
, message, customDimensions


// cancelled report execution (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Cancellation report generation" // do note that in a later version of the schema, this field will not be used   
or customDimensions.eventId == "RT0007" // introduced in version 16.1
| extend aadID = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
, cancelReason = case(
  message contains "The number of processed rows exceeded", "MaxRowsExceeded"
, message contains "The action took longer to complete", "MaxTimeExceeded"
, message contains "Received a cancellation request from the user", "UserCancelled"
, "unknown"
)
, message, customDimensions

