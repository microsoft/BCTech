// KQL samples - Job Queue

// Job queue entry enqueued (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000E24' // for performance
| where customDimensions.eventId == 'AL0000E24'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, alJobQueueId = customDimensions.alJobQueueId 	
, alJobQueueIsRecurring = customDimensions.alJobQueueIsRecurring 	
, alJobQueueObjectId = customDimensions.alJobQueueObjectId 	
, alJobQueueObjectName = customDimensions.alJobQueueObjectName // added in 22.0 
, alJobQueueObjectType = customDimensions.alJobQueueObjectType
, alJobQueueObjectDescription = customDimensions.alJobQueueObjectDescription // added in 22.0 
, alJobQueueStatus = customDimensions.alJobQueueStatus


// Job queue entry started (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000E25' // for performance
| where customDimensions.eventId == 'AL0000E25'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, alJobQueueId = customDimensions.alJobQueueId 	
, alJobQueueObjectId = customDimensions.alJobQueueObjectId 	
, alJobQueueObjectName = customDimensions.alJobQueueObjectName // added in 22.0 
, alJobQueueObjectType = customDimensions.alJobQueueObjectType
, alJobQueueObjectDescription = customDimensions.alJobQueueObjectDescription // added in 22.0 
, alJobQueueStatus = customDimensions.alJobQueueStatus


// Job queue entry finished (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000E26' // for performance
| where customDimensions.eventId == 'AL0000E26'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, alJobQueueId = customDimensions.alJobQueueId 	
, alJobQueueObjectId = customDimensions.alJobQueueObjectId 	
, alJobQueueObjectName = customDimensions.alJobQueueObjectName // added in 22.0 
, alJobQueueObjectType = customDimensions.alJobQueueObjectType
, alJobQueueObjectDescription = customDimensions.alJobQueueObjectDescription // added in 22.0 
, alJobQueueStatus = customDimensions.alJobQueueStatus
, alJobQueueExecutionTimeInMs = customDimensions.alJobQueueExecutionTimeInMs
, alJobQueueStatus = iff( isempty(customDimensions.alJobQueueStatus), customDimensions.alJobQueueResult, customDimensions.alJobQueueStatus )
// , alJobQueueStatus = customDimensions.alJobQueueResult // prior to 20.0
// , alJobQueueStatus = customDimensions.alJobQueueStatus // 20.0 or later
 

// Job queue entry failed (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000HE7' // for performance
| where customDimensions.eventId == 'AL0000HE7'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, alJobQueueId = customDimensions.alJobQueueId 	
, alJobQueueObjectId = customDimensions.alJobQueueObjectId 	
, alJobQueueObjectName = customDimensions.alJobQueueObjectName // added in 22.0 
, alJobQueueObjectType = customDimensions.alJobQueueObjectType
, alJobQueueObjectDescription = customDimensions.alJobQueueObjectDescription // added in 22.0 
, alJobQueueStatus = customDimensions.alJobQueueStatus
, alJobQueueExecutionTimeInMs = customDimensions.alJobQueueExecutionTimeInMs
, alJobQueueStacktrace = customDimensions.alJobQueueStacktrace // stack trace added in 21.3
, taskId = customDimensions.alJobQueueScheduledTaskId // you can join to task scheduler telemetry on the taskId


// Job queue entry rescheduled on login: {guid}
// Introduced in version x.y
// pending docs
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000I49' // for performance
| where customDimensions.eventId == 'AL0000I49'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, alJobQueueCompanyName = customDimensions.alJobQueueCompanyName
, alJobQueueEarliestStartDateTime = customDimensions.alJobQueueEarliestStartDateTime
, alJobQueueIsRecurring = customDimensions.alJobQueueIsRecurring
, alJobQueueId = customDimensions.alJobQueueId
, alJobQueueStatus = customDimensions.alJobQueueStatus
, alJobQueueScheduledTaskId = customDimensions.alJobQueueScheduledTaskId
, alJobQueueObjectType = customDimensions.alJobQueueObjectType
, alJobQueueObjectId = customDimensions.alJobQueueObjectId
, alJobQueueRescheduledNewTask = customDimensions.alJobQueueRescheduledNewTask


// Emitted when the "Run once (Foreground)" action is clicked in Job Queue Entries/Card pages
// Used to see which JQs were run in the foreground specifically through the JQ pages
// Introduced in v19.0
// pending docs
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000FMG' // for performance
| where customDimensions.eventId == 'AL0000FMG'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
// NB! dimension names will be changed in 22.x
, alIsRecurring = customDimensions.alIsRecurring
, alId = customDimensions.alId
, alStatus = customDimensions.alStatus
, alEarliestStartDateTime = customDimensions.alEarliestStartDateTime



// Emitted when the JQ fails for the very last time before being set in `Error` state.
// This is used for partners to know how many JQs are running into the final failed state and require manual intervention (restarting etc)
// introduced in v21.5 
// pending docs
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000JRG' // for performance
| where customDimensions.eventId == 'AL0000JRG'
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, alJobQueueId = customDimensions.alJobQueueId 	
, alJobQueueObjectId = customDimensions.alJobQueueObjectId 	
, alJobQueueObjectName = customDimensions.alJobQueueObjectName // added in 22.0 
, alJobQueueObjectType = customDimensions.alJobQueueObjectType
, alJobQueueObjectDescription = customDimensions.alJobQueueObjectDescription // added in 22.0 
, alJobQueueStatus = customDimensions.alJobQueueStatus
, alJobQueueExecutionTimeInMs = customDimensions.alJobQueueExecutionTimeInMs
, alJobQueueStacktrace = customDimensions.alJobQueueStacktrace
, taskId = customDimensions.alJobQueueScheduledTaskId // you can join to task scheduler telemetry on the taskId


// join job queue telemetry with task scheduler telemetry to learn more about the root cause of the error
let job_queue_errors = 
traces
| where timestamp >= ago(30d) // adjust as needed
| where customDimensions.eventId == 'AL0000HE7'
| where customDimensions has 'AL0000HE7' // for performance
| where isnotempty(customDimensions.aadTenantId) // filter away signal from Docker sandboxes
| extend 
  AadTenantId = tostring( customDimensions.aadTenantId )
, CompanyName = tostring( customDimensions.companyName )  
, EnvironmentName = tostring( customDimensions.environmentName )
, EnvironmentType = tostring( customDimensions.environmentType )
, ObjectId = toint(customDimensions.alJobQueueObjectId)
| extend TenantId = strcat( toupper(AadTenantId), toupper(EnvironmentName) )
| project timestamp
, AadTenantId, EnvironmentName, CompanyName, EnvironmentType, TenantId
, PlatformVersion = tostring( customDimensions.componentVersion )
, ObjectId
, ObjectType = customDimensions.alJobQueueObjectType
, ObjectName = customDimensions.alJobQueueObjectName // added in 22.0 
, ObjectDescription = customDimensions.alJobQueueObjectDescription // added in 22.0 
, CodeOwnershipKey = case(
      ObjectId between (50000 .. 99999), 'ENVIRONMENT'
    , ObjectId between (1000000 .. 69999999), 'ISV_ONPREM'
    , ObjectId between (70000000 .. 74999999), 'ISV_ONLINE'
    , 'MICROSOFT'
)
, TaskId = tostring( customDimensions.alJobQueueScheduledTaskId )
, ALStacktrace = customDimensions.alJobQueueStacktrace
;
let task_scheduler_errors = 
traces
| where timestamp >= ago(30d) // adjust as needed
| where customDimensions.eventId == 'LC0045'
| project TaskId = tostring( customDimensions.taskId )
, exception = customDimensions.failureReason
, retryMessage = tostring( customDimensions.retryMessage )
| extend exceptionIsRetriable = case (
    retryMessage has 'Exception is not retriable', false
  , retryMessage has 'Exception is retriable', true
  , false
)
| project-away retryMessage 
;
job_queue_errors
| join kind=leftouter task_scheduler_errors on $left.TaskId == $right.TaskId
| project-away TaskId, TaskId1