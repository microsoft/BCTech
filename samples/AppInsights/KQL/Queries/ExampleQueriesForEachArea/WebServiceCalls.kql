// KQL samples - Incoming Web Service Calls


// Incoming Web Service Requests (RAW data)
traces
| where timestamp > ago(60d) // change if your retention policy is different than the default
| where operation_Name == "Web Services Call" // do note that in a later version of the schema, this field will not be used 
     or customDimensions.eventId == "RT0008" // starting from version 16.1, the eventId is used to identity signal types
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, category = customDimensions.category
, endpoint = customDimensions.endpoint
, httpStatusCode = customDimensions.httpStatusCode // httpStatusCode available from 16.3
, httpHeaders = customDimensions.httpHeaders       // httpHeaders available from 16.3
, httpMethod = customDimensions.httpMethod // httpMethod available from 16.3
, diagnosticsMessage = customDimensions.diagnosticsMessage // Only logged in case of an error in a OData/API call. diagnosticsMessage available from 22.0
, failureReason = customDimensions.failureReason // Only logged in case of an error in a OData/API call. failureReason available from 22.0
, executionTime = customDimensions.serverExecutionTime
, requestQueueTime = customDimensions.requestQueueTime // This dimension was introduced in Business Central 2023 release wave 1, version 22.0.
// the datatype for executionTime and requestQueueTime is timespan so need to convert to milliseconds
, requestQueueTimeMS = toreal(totimespan(customDimensions.requestQueueTime))/10000
, executionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 
, requestTotalTimeMS = ( toreal(totimespan(customDimensions.totalTime))+toreal(totimespan(customDimensions.requestQueueTime)) )/10000
// these lines illustrate how to extract data from the httpHeaders dimension
| extend httpHeadersTmp =  tostring( httpHeaders)
| extend httpHeadersJSON = parse_json(httpHeadersTmp)
| extend msUserAgent = tostring( httpHeadersJSON.['ms-dyn-useragent'] )
| extend httpAuthorization = tostring( httpHeadersJSON.['Authorization'] ) // Authorization header (truncated) available from 17.3



// Web service metadata error: The record with name <record name> was skipped.
// available from 22.2
traces
| where timestamp > ago(60d) // change if your retention policy is different than the default
| where customDimensions.eventId == "RT0035" 
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, webServiceRecordName = customDimensions.webServiceRecordName
, webServiceRecordId = customDimensions.webServiceRecordId


// Web service metadata error: Table field name error, the page contaning the field with caption <caption> was skipped.
// available from 22.2
traces
| where timestamp > ago(60d) // change if your retention policy is different than the default
| where customDimensions.eventId == "RT0036" 
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, metadataStack = customDimensions.metadataStack
, metaFieldId = customDimensions.metaFieldId


// Web service metadata warning: Multiple options while generation navigation property for tablerelation.
// available from 22.2
traces
| where timestamp > ago(60d) // change if your retention policy is different than the default
| where customDimensions.eventId == "RT0038" 
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, otherNavigationProperties = customDimensions.otherNavigationProperties
, pickedNavigationPropertyType = customDimensions.pickedNavigationPropertyType



// Coming in 22.2 (June 2023)
     //    public const string APIMetadataInvalidEntity = "RT0033";
     //    public const string APIMetadataInvalidControl = "RT0034";
     //    public const string APIMetadataInvalidPage = "RT0037";
     //    public const string APIMetadataInvalidWebServiceMetadata = "RT0039";