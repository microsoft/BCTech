// KQL samples - Incoming Web Service Calls


// Incoming Web Service Requests (RAW data)
traces
| where timestamp > ago(60d) // change if your retention policy is different than the default
| where operation_Name == "Web Services Call" // do note that in a later version of the schema, this field will not be used 
     or customDimensions.eventId == "RT0008" // starting from version 16.1, the eventId is used to identity signal types
| project timestamp
, aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, category = customDimensions.category
, endpoint = customDimensions.endpoint
, httpStatusCode = customDimensions.httpStatusCode // httpStatusCode only available from 16.3
, httpHeaders = customDimensions.httpHeaders       // httpHeaders only available from 16.3
, httpMethod // httpMethod only available from 16.3
, httpResponseMessage = customDimensions.httpResponseMessage // // This dimension was introduced in Business Central 2023 release wave 1, version 22.0.
, executionTime = customDimensions.serverExecutionTime
, executionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, requestQueueTime = customDimensions.requestQueueTime // This dimension was introduced in Business Central 2023 release wave 1, version 22.0.
, requestTotalTime = ( toreal(totimespan(customDimensions.totalTime))+toreal(totimespan(customDimensions.requestQueueTime)) )/10000 
// these lines illustrate how to extract data from the httpHeaders dimension
| extend httpHeadersTmp =  tostring( httpHeaders)
| extend httpHeadersJSON = parse_json(httpHeadersTmp)
| extend msUserAgent = tostring( httpHeadersJSON.['ms-dyn-useragent'] )
| extend httpAuthorization = tostring( httpHeadersJSON.['Authorization'] ) // Authorization header (truncated) available from 17.3
