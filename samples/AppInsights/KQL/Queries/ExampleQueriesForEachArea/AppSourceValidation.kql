// KQL samples - App source validation events

// Available in app telemetry (not environment telemetry) from version 18.4

// Use renames in the project operator if you don't need to filter on the columns extracted from customDimensions
// move columns up to the extend operator and add a tostring function call if you need to filter or summarize on it

// to follow all events in a particular request, use this query
// note that events have different custom dimensions, so some columns might be empty for some rows
let _requestId = "e31b8fb1-f790-457f-8a07-f3ae9bc411b2";
traces
| extend requestId = customDimensions.validationRequestId
| where requestId == _requestId
| extend eventId = customDimensions.eventId
| extend failureReason = customDimensions.failureReason
| extend countryRegions = customDimensions.countryRegions
| extend versions = customDimensions.versions 
| extend countryRegion = customDimensions.countryRegion
| extend version = customDimensions.version
| extend extensionName = customDimensions.extensionName
| extend extensionId = customDimensions.extensionId
| extend extensionVersion = customDimensions.extensionVersion
| extend extensionPublisher = customDimensions.extensionPublisher
| extend extensions = customDimensions.extensions
| extend allExtensions = customDimensions.allExtensions
| extend baselineExtensions = customDimensions.baselineExtensions
| extend diagnosticCode = customDimensions.diagnosticCode
| extend diagnosticMessage = customDimensions.diagnosticMessage
| extend diagnosticSeverity = customDimensions.diagnosticSeverity
| extend severity = customDimensions.severity
| project timestamp, requestId, severityLevel, severity
, eventId, message
, countryRegion, versions, countryRegions, version
, extensionName, extensionId, extensionPublisher, extensionVersion
, extensions, allExtensions, baselineExtensions, failureReason, diagnosticCode, diagnosticMessage, diagnosticSeverity, customDimensions
| order by timestamp asc


// this query calculates validation request results
let validation_request_started =
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0028' 
| project start_timestamp = timestamp
, requestId = tostring( customDimensions.validationRequestId )
, countryRegions = customDimensions.countryRegions
, versions = customDimensions.versions 
, extensions = customDimensions.extensions
, customDimensions
;
let finished_successfully = 
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0029' 
| project requestId_success = tostring( customDimensions.validationRequestId )
;
let finished_with_failures = 
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0035' 
| project requestId_failure = tostring( customDimensions.validationRequestId )
, failureReason = customDimensions.failureReason
;
validation_request_started
| join kind=leftouter finished_successfully on $left.requestId == $right.requestId_success
| join kind=leftouter finished_with_failures on $left.requestId == $right.requestId_failure
, result = case(
  isnotempty( requestId_success ), 'Success'
, isnotempty( requestId_failure ), 'Failure'
, customDimensions.do_not_exist
)
| project-away requestId_failure, requestId_success



// this query calculates (Version, country-region) validation results
let validation_request_started =
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0030' 
| project start_timestamp = timestamp
, requestId = tostring( customDimensions.validationRequestId )
, countryRegion = tostring( customDimensions.countryRegion )
, version = tostring( customDimensions.version )
, extensions = customDimensions.extensions
, allExtensions = customDimensions.allExtensions
, baselineExtensions = customDimensions.baselineExtensions
, customDimensions
;
let finished_successfully = 
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0031' 
| project countryRegion_success = tostring( customDimensions.countryRegion )
, version_success = tostring( customDimensions.version )
, requestId_success = tostring( customDimensions.validationRequestId )
;
let finished_with_failures = 
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0036' 
| project countryRegion_failure = tostring( customDimensions.countryRegion )
, version_failure = tostring( customDimensions.version )
, requestId_failure = tostring( customDimensions.validationRequestId )
, failureReason = customDimensions.failureReason
;
validation_request_started
| join kind=leftouter finished_successfully on $left.requestId == $right.requestId_success and $left.countryRegion == $right.countryRegion_success and $left.version == $right.version_success
| join kind=leftouter finished_with_failures on $left.requestId == $right.requestId_failure and $left.countryRegion == $right.countryRegion_failure and $left.version == $right.version_failure
| extend end_timestamp = case(
  isnotempty( requestId_success ), timestamp_success
, isnotempty( requestId_failure ), timestamp_failure
, customDimensions.do_not_exist
)
, result = case(
  isnotempty( requestId_success ), 'Success'
, isnotempty( requestId_failure ), 'Failure'
, customDimensions.do_not_exist
)
| project-away requestId_failure, requestId_success, countryRegion_success, version_success, countryRegion_failure, version_failure




//
// raw event queries start here
//

// AppSource submission validation request started
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0028' 
| project timestamp
, requestId = customDimensions.validationRequestId
, countryRegions = customDimensions.countryRegions
, versions = customDimensions.versions 
, extensions = customDimensions.extensions
, message


// AppSource submission validation request completed successfully
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0029' 
| project timestamp 
, requestId = customDimensions.validationRequestId
, countryRegions = customDimensions.countryRegions
, versions = customDimensions.versions 
, extensions = customDimensions.extensions
, message


// (Version, country-region) validation started
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0030' 
| project timestamp
, requestId = customDimensions.validationRequestId
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensions = customDimensions.extensions
, allExtensions = customDimensions.allExtensions
, baselineExtensions = customDimensions.baselineExtensions
, message


// (Version, country-region) validation completed successfully
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0031' 
| project timestamp
, requestId = customDimensions.validationRequestId
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensions = customDimensions.extensions
, allExtensions = customDimensions.allExtensions
, baselineExtensions = customDimensions.baselineExtensions
, message


// Extension validation started
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0032' 
| project timestamp 
, requestId = customDimensions.validationRequestId
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensionName = customDimensions.extensionName
, message


// Extension validation completed successfully
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0033' 
| project timestamp
, requestId = customDimensions.validationRequestId
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensionName = customDimensions.extensionName
, message


// Validation diagnostic reported
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0034' 
| project timestamp 
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionPublisher = customDimensions.extensionPublisher
, extensionVersion = customDimensions.extensionVersion
, diagnosticCode = customDimensions.diagnosticCode
, diagnosticMessage = customDimensions.diagnosticMessage
, diagnosticSeverity = customDimensions.diagnosticSeverity
, diagnosticSourceLocation = customDimensions.diagnosticSourceLocation
, diagnosticSourcePath = customDimensions.diagnosticSourcePath
, message = customDimensions.message


// AppSource submission validation request completed with failures
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0035' 
| project timestamp 
, requestId = customDimensions.validationRequestId
, countryRegions = customDimensions.countryRegions
, versions = customDimensions.versions 
, extensions = customDimensions.extensions
, failureReason = customDimensions.failureReason
, message


// (Version, country-region) validation completed with failures
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0036' 
| project timestamp 
, requestId = customDimensions.validationRequestId
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensions = customDimensions.extensions
, allExtensions = customDimensions.allExtensions
, baselineExtensions = customDimensions.baselineExtensions
, failureReason = customDimensions.failureReason
, message


// Extension validation completed with failures
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0037' 
| project timestamp 
, requestId = customDimensions.validationRequestId
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensionName = customDimensions.extensionName
, failureReason = customDimensions.failureReason
, message


// Diagnostic reported on AppSource submission validation request: {validationRequestId}
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0038' 
| project timestamp 
, requestId = customDimensions.validationRequestId
, diagnosticCode = customDimensions.diagnosticCode
, diagnosticMessage = customDimensions.diagnosticMessage
, diagnosticSeverity = customDimensions.diagnosticSeverity
, containsHotfix = customDimensions.containsHotfix
, message



// Extension validations 
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0033' 
| project timestamp
, requestId = customDimensions.validationRequestId
, result = 'Success'
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensionName = customDimensions.extensionName
| union
(
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions.eventId == 'LC0033' 
| project timestamp
, result = 'Failure'
, requestId = customDimensions.validationRequestId
, countryRegion = customDimensions.countryRegion
, version = customDimensions.version
, extensionName = customDimensions.extensionName
)
