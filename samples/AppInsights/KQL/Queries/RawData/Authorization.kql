// KQL samples - Authorization

// Use renames in the project operator if you don't need to filter on the columns extracted from customDimensions
// move columns up to the extend operator and add a tostring function call if you need to filter or summarize on it

// successful authorization prior to openCompany trigger (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Authorization Succeeded (Pre Open Company)"  // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
     or customDimensions.eventId == "RT0003" // starting from version 16.1, the eventId is used to identity signal types
| extend aadTenantId = customDimensions.aadTenantId
, eventId = customDimensions.eventId 
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, guestUser = customDimensions.guestUser
, userType = customDimensions.userType
, entitlementSetIds = customDimensions.entitlementSetIds


// failed authorization prior to openCompany trigger (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Authorization Failed (Pre Open Company)"  // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
     or customDimensions.eventId == "RT0001" // starting from version 16.1, the eventId is used to identity signal types
| extend aadTenantId = customDimensions.aadTenantId
, eventId = customDimensions.eventId 
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, guestUser = customDimensions.guestUser
, userType = customDimensions.userType
, failureReason = customDimensions.failureReason
, entitlementSetIds = customDimensions.entitlementSetIds


// successful authorization after the openCompany trigger (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Authorization Succeeded (Open Company)"  // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
     or customDimensions.eventId == "RT0004" // starting from version 16.1, the eventId is used to identity signal types
| extend aadTenantId = customDimensions.aadTenantId
, eventId = customDimensions.eventId 
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, clientType = customDimensions.clientType
, companyName = customDimensions.companyName
, result = customDimensions.result
// server timings are available from 16.0
, sqlExecutes = customDimensions.sqlExecutes
, executionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000
, sqlRowsRead = customDimensions.sqlRowsRead
, totalTime = customDimensions.totalTime
| extend serverExecutionTime = customDimensions.serverExecutionTime


// failed authorization after the openCompany trigger (raw data)
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == "Authorization Failed (Open Company)"  // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
     or customDimensions.eventId == "RT0002" // starting from version 16.1, the eventId is used to identity signal types
| extend aadTenantId = customDimensions.aadTenantId
, eventId = customDimensions.eventId 
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, clientType = customDimensions.clientType
, companyName = customDimensions.companyName
, failureReason = customDimensions.failureReason
// server timings are available from 16.0
, sqlExecutes = customDimensions.sqlExecutes
, executionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000
, sqlRowsRead = customDimensions.sqlRowsRead
, totalTime = customDimensions.totalTime
| extend serverExecutionTime = customDimensions.serverExecutionTime


// client type is not known pre-open company
// You need to join RT0001 to RT0002/RT0004 if you need both userType and clientType
let authStepOne =  
traces
| where 1==1 
and timestamp > ago(1d) // adjust as needed
| where customDimensions.eventId in ('RT0001', 'RT0003')
| extend aadTenantId = tostring( customDimensions.aadTenantId)
, environmentName = tostring( customDimensions.environmentName)
, environmentType = tostring( customDimensions.environmentType)
, platformVersion = tostring(customDimensions.componentVersion)
, userType = tostring(customDimensions.userType)
| project timestamp
, aadTenantId, environmentName, environmentType
, platformVersion
, userType
, operation_Id
;
let authStepTwo =  
traces
| where 1==1 
and timestamp > ago(1d) // adjust as needed
| where customDimensions.eventId in ('RT0002', 'RT0004')
| extend clientType = tostring(customDimensions.clientType)
| project timestamp
, clientType
, operation_Id
;
authStepOne
| join kind=leftouter authStepTwo on $left.operation_Id == $right.operation_Id
| project-away timestamp1, operation_Id, operation_Id1
